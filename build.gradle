
plugins {
    id 'maven-publish'
    id "xyz.wagyourtail.unimined" version "1.2.0-SNAPSHOT"
    // id 'org.gradle.toolchains.foojay-resolver-convention' version '0.5.0'
}

apply plugin: 'idea'

version = mod_version
group = project_pkg
archivesBaseName = "${mod_id}-${minecraft_version}"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

unimined.minecraft {
    version project.minecraft_version

    mappings {
        mojmap()
        parchment("2023.09.03")
    }

    minecraftForge {
        loader project.forge_version
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        url 'https://www.cursemaven.com'
        content {
            includeGroup "curse.maven"
        }
    }

    maven {
        url "https://mcentral.firstdark.dev/releases"
    }

    flatDir {
        dir 'libs'
    }
}

dependencies {

    modImplementation ("curse.maven:pipez-443900:4713656")
    modImplementation ("curse.maven:refined-storage-${refinedstorage_mod_id}:4844585")
    modImplementation ("curse.maven:mekanism-${mekanism_mod_id}:4866575")

    // Loves mixins
    modImplementation ("curse.maven:applied-energistics2-223794:4857895")

//    modImplementation ("curse.maven:cofh-core-69162:4970669")
//    modImplementation ("curse.maven:thermal-dynamics-227443:4970699")
//    modImplementation ("curse.maven:thermal-foundation-222880:4979840")

    modImplementation ("curse.maven:pretty-pipes-376737:4769646")
    modImplementation ("curse.maven:jei-238222:4712868")
    modImplementation ("curse.maven:mcjtylib-233105:4747680")
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : mod_name,
                "Specification-Vendor"    : mod_vendor,
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : mod_name,
                "Implementation-Version"  : mod_version,
                "Implementation-Vendor"   : mod_vendor,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}


tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            minecraft_version: minecraft_version, minecraft_version_range: minecraft_version_range,
            forge_version: forge_version, forge_version_range: forge_version_range,
            loader_version_range: loader_version_range,
            mod_id: mod_id, mod_name: mod_name, mod_version: mod_version
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}